plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
	id "org.sonarqube" version "7.2.2.6593"
	id 'jacoco'
}

sonar {
  properties {
    property "sonar.projectKey", "100-hours-a-week-11team-be"
    property "sonar.organization", "100-hours-a-week"
  }
}

jacoco {
    toolVersion = "0.8.12"
}

group = 'com.thunder11'
version = findProperty('releaseVersion') ?: '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'com.h2database:h2'
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests'
    group = 'verification'
	useJUnitPlatform()
}

tasks.named('test') {
	useJUnitPlatform()
}

jacocoTestReport {
    dependsOn integrationTest
    executionData file("$buildDir/jacoco/integrationTest.exec")

	onlyIf { file("$buildDir/jacoco/integrationTest.exec").exists() }

    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    executionData file("$buildDir/jacoco/integrationTest.exec")

	onlyIf { file("$buildDir/jacoco/integrationTest.exec").exists() }

    violationRules {
        rule {
            limit {
                minimum = (project.findProperty("COVERAGE_MIN") ?: "0.0") as BigDecimal
            }
        }
    }
}